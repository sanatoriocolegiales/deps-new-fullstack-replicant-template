(ns {{top/ns}}.{{main/ns}}.front-end.core
  (:require [replicant.dom :as r]
            [nexus.registry :as nxr]
            [nexus.strategies :as strategies]
            [tick.core :as time]
            [datascript.core :as ds]
            [taoensso.telemere :as t]
            [replicant.alias :as alias]
            [com.wsscode.transito :as transito]
            [com.wsscode.pathom3.connect.operation.transit :as pcot] 
            [{{top/ns}}.{{main/ns}}.front-end.enrutador :as enrutador]
            [{{top/ns}}.{{main/ns}}.front-end.frontpage :as frontpage]
            [{{top/ns}}.{{main/ns}}.front-end.db.store :as db-store :refer [get-store]]))

(defn realizar-consulta
  ([estado consulta]
   (realizar-consulta estado consulta nil))
  ([estado consulta atributo]
   (t/event! ::realiza-consulta {:level :info
                                 :data {:estado estado
                                        :atributo atributo
                                        :consulta consulta}}) 
   (db-store/persistir! estado (db-store/send-request -1 (time/now) consulta))
   (let [ident (db-store/get-query-id @estado consulta)
         pathom-query (cond-> {:pathom/eql consulta}
                        atributo (assoc :pathom/entity atributo))] 
     (-> (js/fetch (str js/location.origin "/v1/api") (clj->js {:method "POST"
                                                                :headers {"Content-Type" "application/transit+json"
                                                                          "Accept" "application/transit+json"}
                                                                :body (transito/write-str pathom-query {:handlers pcot/write-handlers})}))
         (.then #(.text %))
         (.then #(transito/read-str % {:handlers (assoc pcot/read-handlers "f" (fn [t]
                                                                                 (let [v (js/Object.values t)]
                                                                                   (->> v
                                                                                        js->clj
                                                                                        (apply str)
                                                                                        js/Number.parseInt))))}))
         (.then #(db-store/persistir! estado (db-store/receive-response ident (time/now) consulta %)))
         (.catch #(let [mensaje (.-message %)] 
                    (t/event! ::error-al-realizar-consulta {:level :error
                                                            :data {:excepcion mensaje
                                                                   :consulta consulta
                                                                   :atributo atributo}})
                    (db-store/persistir! estado (db-store/receive-response ident (time/now) consulta {:success? false
                                                                                                   :error mensaje}))
                    (throw %)))))))

(defn routing-anchor [attrs children]
  (let [routes (-> attrs :replicant/alias-data :routes)]
    (into [:a (cond-> attrs
                (:ui/location attrs)
                (assoc :href (enrutador/location->url routes (:ui/location attrs))))]
          children)))

(alias/register! :ui/a routing-anchor)

(defn find-target-href [e]
  (some-> e .-target
          (.closest "a")
          (.getAttribute "href")))

(defn get-input-value [^js element]
  (cond
    (= "number" (.-type element)) (when (not-empty (.-value element))
                                    (.-valueAsNumber element))
    :else (.-value element)))

(defn get-input-key [^js element]
  (when-let [k (some-> element .-name not-empty keyword)]
    (when (or (not= "checkbox" (.-type element))     
              (.-checked element)                    
              (not (.hasAttribute element "value"))) 
      k)))

(defn gather-form-data [^js form-el]
  (some-> (.-elements form-el)
          into-array
          (.reduce
           (fn [res ^js el]
             (let [k (get-input-key el)]
               (cond-> res
                 k (assoc k (get-input-value el)))))
           {})))          

(def pages
  [frontpage/page])

(def by-page-id
  (->> pages
       (map (juxt :page-id identity))
       (into {})))

(defn get-render-f [state] 
  (or (get-in by-page-id [(-> (db-store/get-current-location state) :ui/location :location/page-id) :render])
      (fn [_] 
        [:h1 "No se encontrÃ³ la ruta especificada"]))) 
 
(defn get-current-location [routes]
  (->> js/location.pathname
       (enrutador/url->location routes)))

(defn navigate! [estado new-location]
  (when new-location
    (db-store/persistir! estado [{:db/ident :ui/location
                                  :ui/location new-location}])))

(defn route-click [e estado routes]
  (let [href (find-target-href e)]
    (println "href: " href)
    (when-let [location (enrutador/url->location routes href)]
      (println "location: " location)
      (.preventDefault e)
      (if (enrutador/essentially-same? location (:ui/location (db-store/get-current-location estado)))
        (.replaceState js/history nil "" href)
        (.pushState js/history nil "" href))
      (navigate! estado location))))

(nxr/register-system->state! ds/db)

(nxr/register-interceptor! strategies/fail-fast) 

(nxr/register-effect! 
 :db/transact
 ^:nexus/batch
 (fn [_ db data]
   (when db
     (try
       (db-store/persistir! db (apply concat (map first data)))
       (catch :default e (t/event! ::error-al-realizar-consulta {:level :error
                                                                 :data {:excepcion (.-message e)
                                                                        :args data}}))))))

(nxr/register-effect!
 :data/query
 (fn [_ estado consulta atributo]
   (try 
     (if atributo
       (realizar-consulta estado consulta atributo)
       (realizar-consulta estado consulta))
     (catch :default e (t/event! ::error-al-ejecutar-consulta-al-backend {:level :error
                                                                          :data (.-message e)})))))

(nxr/register-action!
 :db/load
 (fn [_ consulta atributo]
   (if atributo 
     [[:data/query consulta atributo]]
     [[:data/query consulta]])))

(nxr/register-placeholder!
 :event.target/value
 (fn [{:replicant/keys [dom-event]}]
   (some-> dom-event .-target .-value)))

(nxr/register-placeholder!
 :event/prevent-default
 (fn [{:replicant/keys [dom-event]}]
   (some-> dom-event .preventDefault)))

(nxr/register-placeholder!
 :parse/number
 (fn [_ val]
   (or (some-> val parse-long) 0)))

(nxr/register-placeholder!
 :event/form-data
 (fn [{:replicant/keys [dom-event]}] 
   (some-> dom-event .-target gather-form-data)))   

(defn main [estado el]
  (let [routes (enrutador/make-routes pages)]
    (add-watch estado ::render (fn [_ _ _ _]
                                 (let [state (ds/db estado)
                                       f (get-render-f estado)]
                                   (println {:render-fn (meta f)
                                             :estado state})
                                   (r/render el (f state) {:alias-data {:routes routes}}))))
    
    (r/set-dispatch! (fn [data actions] 
                       (nxr/dispatch estado data actions)))

    (js/document.body.addEventListener
     "click"
     (fn [e]
       (println "Evento click")
       (route-click e estado routes)))

    (js/window.addEventListener
     "popstate"
     (fn [_] 
       (println "Evento popstate")
       (navigate! estado (get-current-location routes))))

    (db-store/persistir! estado [{:db/ident :system/app
                                  :app/cargado-a-las (time/time)}
                                 (get-current-location routes)])

    (navigate! estado (get-current-location routes))))

(defn init
  []
  (main (get-store) js/document.body))


(comment
  
  (t/with-signal
    (t/event! ::test {:a 1
                      :b 2}))
  
  (def s (get-store))

  (keys (ds/db dev/store))

  (:eavt (ds/db dev/store))
  (:aevt (ds/db dev/store))
  (:avet (ds/db dev/store))

  (main s js/document.body)

  (let [mensaje "Bla bla bla"
        app 2323
        consulta [:internados]
        estado store]
    #_(receive-response app (time/now) consulta {:success? false
                                                 :error mensaje})
    (ds/transact! estado (receive-response app (time/now) consulta {:success? false
                                                                    :error mensaje}))
    (ds/transact! estado (receive-response app (time/now) consulta {:internados [:registros :registros :registros]}))
    (ds/transact! estado [{:db/ident :ui/location
                           :location :front-page}])
    (ds/transact! estado [{:db/ident :system/app
                           :app/cargado-a-las (time/time)}]))

  (ds/q '[:find ?status
          :in $ ?q
          :where
          [?e :query/query ?q]
          [?e :query/status ?status]]
        @store
        [:internados])

  (tap> @dev/store)

  (ds/q '[:find ?a
          :where
          [?e :db/id 2323]
          [?e :query/query [:internados]]
          [?e :query/status ?a]]
        @store)

  (ds/pull @store [:query/status :query/query :query/user-time :query/result] 2323)

  (ds/pull @store [:*] 2323)

  (ds/pull @store [:location] :ui/location)

  (ds/entity @store :system/app)

  (ds/entity @store :ui/location)

  (some->
   (ds/q '[:find ?id
           :in $ ?q
           :where
           [?e :db/id ?id]
           [?e :query/query ?q]]
         @store
         [:internados])
   first)

   by-page-id
  :rcf)